# Fps tutorial

## Tutorial

On MeshInstance3D, add children MeshInstance3D -> click on mesh, create collision shape, simplified convex

_creates optimized collision shape for the mesh_

![collision-add.PNG](/collision-add.PNG)

On world add directionalLight3D and WorldEnvironment

![world add.PNG](/world%20add.PNG)

When adding the CSBox3D check Use Collision

Add script to the player. (by default has the movement and jump)

Camera must be put in Node3D for [reasons](https://docs.godotengine.org/en/latest/tutorials/3d/using_transforms.html)

Add controllers: Project -> Project settings -> Input Map
Add map object for testing: check usecollision -> select newShaderMaterial -> albedo to modify the color

### All players code
```js
// capture mouse
func _ready() -> void:
    Input.set_mouse_mode(Input.MOUSE_MODE_CAPTURED)
```

```godotengine [Player.gd]
extends CharacterBody3D


var speed
const JUMP_VELOCITY = 4.5
const WALK_SPEED = 5.8
const SPRINT_SPEED = 8.1
const SENSITIVITY = 0.01

# bob variable make the camera jump in places
const BOB_FREQ = 2.0 # how much frequent camera jumps
const BOB_AMP = 0.08 # how much camera jumps
var t_bob = 0.0 # how far we are away from the sign variable

# FOV variables
const BASE_FOV = 75.0
const FOV_CHANGE = 1.5

# reference to nodes, you can drag and drop them
@onready var head = $Head
@onready var camera = $Head/Camera3D

# capture the mouse
func _ready() -> void:
	Input.set_mouse_mode(Input.MOUSE_MODE_CAPTURED)
	
func _unhandled_input(event: InputEvent) -> void:
	if event is InputEventMouseMotion:
		head.rotate_y(-event.relative.x * SENSITIVITY)
		camera.rotate_x(-event.relative.y * SENSITIVITY)
		camera.rotation.x = clamp(camera.rotation.x, deg_to_rad(-40), deg_to_rad((60)))

func _physics_process(delta: float) -> void:
	# Add the gravity.
	if not is_on_floor():
		velocity += get_gravity() * delta

	# Handle jump.
	if Input.is_action_just_pressed("jump") and is_on_floor():
		velocity.y = JUMP_VELOCITY
		
	# Handle Sprint
	if Input.is_action_pressed("run"):
		speed = SPRINT_SPEED
	else: 
		speed = WALK_SPEED

	# Get the input direction and handle the movement/deceleration.
	# As good practice, you should replace UI actions with custom gameplay actions.
	var input_dir := Input.get_vector("left", "right", "up", "down")
	# replce with head
	var direction: Vector3 = (head.transform.basis * Vector3(input_dir.x, 0, input_dir.y)).normalized()
	if is_on_floor(): 
		if direction:
			velocity.x = direction.x * speed
			velocity.z = direction.z * speed
		else:
			velocity.x = lerp(velocity.x, direction.x * speed, delta * 7.0)
			velocity.z = lerp(velocity.z, direction.z * speed, delta * 7.0)
	else:
		velocity.x = lerp(velocity.x, direction.x * speed, delta * 3.0)
		velocity.z = lerp(velocity.z, direction.z * speed, delta * 3.0)
	# head bob
	t_bob += delta * velocity.length() * float(is_on_floor())
	camera.transform.origin = _headbob(t_bob)
	
	# FOV 
	var velocity_clamped = clamp(velocity.length(), 0.5, SPRINT_SPEED * 2)
	var target_fov = BASE_FOV + FOV_CHANGE * velocity_clamped
	camera.fov = lerp(camera.fov, target_fov, delta * 8.0)

	move_and_slide()
	
func _headbob(time) -> Vector3:
	var pos = Vector3.ZERO
	pos.y = sin(time * BOB_FREQ) * BOB_AMP
	pos.x = cos(time * BOB_FREQ / 2) * BOB_AMP
	return pos

```

## Structure
- World
  - DirectionalLight3D
  - WorldEnvironment
  - CSBox3D (floor)
  - Player (CharacterBody3D)
    - MeshInstance3D
    - CollisionShape3D
    - Head (Simple Node3D; Acts as a pivot for camera)
      - Camera3D